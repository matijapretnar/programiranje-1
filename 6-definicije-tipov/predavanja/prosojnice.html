<!DOCTYPE html>
<html>
<head>
<title>Definicije tipov</title>
<meta charset="utf-8">
<link rel="stylesheet" href="../../pomozno/prosojnice.css" />
</head>
<body>
<textarea id="source">

class: center, middle

# Definicije tipov

## Programiranje 1

---

### Za tipe si lahko definiramo okrajšave

.source[```
type r3 = float * float * float
```]

### Tipi lahko vsebujejo tudi parametre

.source[```
type 'a zaporedje = int -> 'a

type ('k, 'v) slovar = ('k * 'v) list
```]

---

### **Zapisni tip** podamo z zahtevanimi polji

.source[```
type kompleksno = {re : float; im : float}
type polarno = {kot : float; radij : float}
```]

.terminal[```
# let i = {re = 0.0; im = 1.0};;
val i : kompleksno = {re = 0.; im = 1.}

# let abs z = sqrt (z.re ** 2. +. z.im ** 2.);;
val abs : kompleksno -> float = <fun>
```]

### .center[zapisi = nabori z imenovanimi polji<br>zapisni tipi = produkti z imenovanimi faktorji
]

---

### **Vsoto** podamo z možnimi **variantami**

.source[```
type barva = Rdeca | Zelena | Modra
```]

.terminal[```
# [Rdeca; Zelena; Zelena];;
- : barva list = [Rdeca; Zelena; Zelena]

# [Rdeca; Zelena; Zelena; Rumena];;
                          ^^^^^^
Error: This variant expression is expected to
have type barva. The constructor Rumena does not
belong to type barva
```]

### `Rdeca`, `Zelena`, `Modra` so **konstruktorji**

---

### Konstruktorji lahko sprejmejo tudi argumente

.source[```
type geometrijski_objekt =
  | Tocka
  | Krog of float
  | Pravokotnik of float * float
```]

.terminal[```
# [Tocka; Pravokotnik (1., 2.); Tocka; Krog 3.];;
- : geometrijski_objekt list = ...
```]

---

### Funkcije na vsotah podamo po kosih

.source[```
type geometrijski_objekt =
  | Tocka
  | Krog of float
  | Pravokotnik of float * float
```]

.source[```
let povrsina = function
  | Tocka -> 0.
  | Krog r -> 3.14 *. r ** 2.
  | Pravokotnik (v, s) -> v *. s
```]

### `match`, `function`, vzorci, … so **dekonstruktorji**


---

### Tudi vsote z eno varianto so koristne

.source[```
type leto = Leto of int
type visina = Visina of int
type stevilo = Stevilo of int
```]

### .center[.mini[![](slike/unnecessary-math.jpg)]]

---

### Tudi vsote z eno varianto so koristne

.source[```
type leto = Leto of int
type visina = Visina of int
type stevilo = Stevilo of int
```]

.terminal[```
# let koliko_let_je_minilo (Leto l) = 2018 - l;;
val koliko_let_je_minilo : leto -> int = <fun>

# koliko_let_je_minilo (Stevilo 200);;
                        ^^^^^^^
Error: This variant expression is expected to
have type leto. The constructor Stevilo does not
belong to type leto
```]


---

class: question, center, middle

## Zakaj vsotam pravimo vsote?

---

### Delno definirane funkcije lahko sprožijo izjeme

.bad-example[```
let slaba_glava (x :: _) = x
```]

.terminal[```
Warning 8: Here is an example of a value that
is not matched: []
val slaba_glava : 'a list -> 'a = <fun>
```]

.terminal[```
# slaba_glava [1; 2; 3];;
- : int = 1
# slaba_glava [];;
Exception: Match_failure.
```]

### Ali obstaja varnejši način?

---

### Za delne funkcije raje uporabimo tip `option`

.source[```
type 'a option = None | Some of 'a
```]

.good-example[```
let glava = function
  | [] -> None
  | x :: _ -> Some x
```]

.terminal[```
val glava : 'a list -> 'a option = <fun>

# glava [1; 2; 3];;
- : int option = Some 1
# glava [];;
- : 'a option = None
```]

---

### Če uporabimo `option`, nas tipi prisilijo, da obravnavamo robne primere

.source[```
let velika_glava xs = glava xs > 100
```]

.terminal[```
Characters 28-36:
  let velika_glava xs = glava xs > 100;;
                        ^^^^^^^^
Error: This expression has type 'a option
but an expression was expected of type int
```]

---

### Če uporabimo `option`, nas tipi prisilijo, da obravnavamo robne primere

.source[```
let velika_glava xs =
  match glava xs with
  | None -> false
  | Some x -> x > 100
```]

.terminal[```
# velika_glava [1000; 2; 3];;
- : bool = true
# velika_glava [50; 2; 3];;
- : bool = false
# velika_glava [];;
- : bool = false
```]

---

### Če uporabimo `option`, nas tipi prisilijo, da obravnavamo robne primere

.source[```
let velika_glava xs =
  match glava xs with
  | None -> None
  | Some x -> Some (x > 100)
```]

.terminal[```
# velika_glava [1000; 2; 3];;
- : bool option = Some true
# velika_glava [50; 2; 3];;
- : bool option = Some false
# velika_glava [];;
- : bool option = None
```]

---

class: question

### Kaj vse je narobe s tem tipom?

.bad-example[```
type izdelek = ..
type naslov = ..
type papir = ..

type narocilo = {
  nar_izdelki : izdelek list;
  hitra_dost : bool;
  nas_prejemnika : naslov;
  darilo : bool;
  vp : papir;
}
```]

---

### Rekurzivnim vsotam/zapisom pogosto pravimo **algebrajski** oziroma **induktivni** tipi

.source[```
type izraz =
  | Stevilo of int
  | Plus of izraz * izraz
  | Minus of izraz
  | Krat of izraz * izraz
```]

### $$-(5 \times (2 + 7))$$

.source[```
Minus (
  Krat (Stevilo 5, Plus (Stevilo 2, Stevilo 7))
)
```]

---

class: question, center, middle

## `izracunaj`

.terminal.left[```
# let x = Plus (Stevilo 2, Stevilo 4);;
val x : izraz = Plus (Stevilo 2, Stevilo 4)

# let y = Minus (Stevilo 6);;
val y : izraz = Minus (Stevilo 6)

# izracunaj (Plus (x, Krat (y, y)));;
- : int = 42
```]

---

class: question, center, middle

## Ali kakšne algebrajske tipe<br>poznamo že od prej?


</textarea>
<script src="../../pomozno/prosojnice.js"></script>
</body>
</html>
