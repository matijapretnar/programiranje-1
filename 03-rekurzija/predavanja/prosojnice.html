<!DOCTYPE html>
<html>
<head>
<title>Funkcije</title>
<meta charset="utf-8">
<link rel="stylesheet" href="../../pomozno/prosojnice.css" />
</head>
<body>
<textarea id="source">

class: center, middle

# Funkcijsko programiranje

## Programiranje 1

---

class: center, middle

## Sistem tipov

---

### Tipi v OCamlu so **statični**

.source[```ocaml
let je_majhen x =
  if x < 10 then "Da" else false
```]

.terminal[```ocaml
Characters 45-50:
  if x < 10 then "Da" else false
                           ^^^^^
Error: This expression has type bool but an
  expression was expected of type string
```]

---

### Tipi v Pythonu so **dinamični**

.source[```python
def je_majhen(x):
    return 'Da' if x < 10 else False
```]

.terminal[```python
>>> je_majhen(3) + '!'
'Da!'

>>> je_majhen(10) + '!'
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
TypeError: unsupported operand type(s)
  for +: 'bool' and 'str'
```]

---

### Tipi v Pythonu so **dinamični**

.source[```python
def fakulteta(n):
    if n <= 1:
        return 1
    else:
        n * fakulteta(n - 1)
```]

--

.terminal[```python
>>> fakulteta(4)
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
  File "<stdin>", line 5, in fakulteta
  File "<stdin>", line 5, in fakulteta
TypeError: unsupported operand type(s)
  for *: 'int' and 'NoneType'
```]

---

### OCamlovi tipi so **bogatejši** od Pythonovih

.terminal[```
# [(1, ['a']); (10, []); (0, ['x'; 'y'])];;
- : (int * char list) list = ...

# fun x -> [(x, 0)];;
- : 'a -> ('a * int) list = <fun>
```]

.terminal[```python
>>> type([(1, ['a']), (10, []), (0, ['x', 'y'])])
<type 'list'>

>>> type(lambda x: [(x, 0)])
<type 'function'>
```]

---

class: center, middle

## Ujemanje vzorcev

---

### Ujemanje izvede **prvo** vejo, pri kateri se<br>vrednost **ujema z vzorcem**

```
match vrednost with
| vzorec1 -> veja1
| vzorec2 -> veja2
| ... -> ...
```

```
function
| vzorec1 -> veja1
| vzorec2 -> veja2
| ... -> ...
```

---

### Vsaka **konstanta** je vzorec

```
function
| true -> "res"
| false -> "ni res"
```

```
function
| 1 -> "ena"
| 2 -> "dva"
```

---

### Vzorec `_` se ujema z **vsemi** vrednostim

```
function
| 1 -> "ena"
| 2 -> "dva"
| _ -> "veliko"
```

---

### Spremenljivka je vzorec, ki se ujema z **vsemi** vrednosti,<br>ter v veji omogoči **dostop do zajete vrednosti**

```
function
| 1 -> "ena"
| 2 -> "dva"
| x -> "število " ^ string_of_int x
```

---

class: question

### **Katera veja** se izvede v spodnjem ujemanju?

```
let x = 3 in
match 4 with
| x -> "Ali se jaz ujemam?"
| y -> "Ali se šele jaz?"
```

---

### Vzorec `(v1, v2, ...)` ustreza **naborom**,<br>pri čemer so `v1`, `v2`, … **nadaljnji** vzorci

```
function
| (0, 0) -> "izhodišče"
| (_, 0) -> "abscisa"
| (0, _) -> "ordinata"
| (_, _) -> "nekje drugje"
```

---

### Vzorec `v when pogoj` se ujema le ob **izpolnjenem pogoju**

```
function
| (0, 0) -> "izhodišče"
| (_, 0) -> "abscisa"
| (0, _) -> "ordinata"
| (x, y) when x = y -> "diagonala lihih kvadrantov"
| (_, _) -> "nekje drugje"
```

#### Zakaj ne pišemo `| (x, x) -> "diagonala lihih kvadrantov"`?

---

### S praznim **seznamom** se ujema `[]`, s sestavljenim pa `v1 :: v2`

```
function
| [] -> "prazen"
| _ :: [] -> "en element"
| _ :: _ :: [] -> "dva elementa"
| _ :: _ :: _ -> "vsaj dva elementa"
```

### Za sezname dane dolžine lahko uporabimo tudi `[v1; v2; ...]`

```
function
| [] -> "prazen"
| [_] -> "en element"
| [_; _] -> "dva elementa"
| _ :: _ :: _ -> "vsaj dva elementa"
```

---

class: center, middle

## Repni klici

---

### Ob klicu funkcije računalnik trenutno delo **odloži na sklad**

.bad-example[```
let f x = 4 * x
let g x = 6 + f x
let h x = 3 * g x
```]

```
  h 2                (* h dela *)
= 3 * g 2            (* g dela, h čaka *)
= 3 * (6 + f 2)      (* f dela, g in h čakata *)
= 3 * (6 + (4 * 2))  (* f dela, g in h čakata *)
= 3 * (6 + 8)        (* g dela, h čaka *)
= 3 * 14             (* h dela *)
= 42
```

---

### Če je klic funkcije **repen**, lahko delo opravimo sproti

.good-example[```
let f x = 3 * x
let g x = f (6 + x)
let h x = g (4 * x)
```]

```
  h 2                (* h dela *)
= g (4 * 2)          (* h dela *)
= g 8                (* g dela *)
= f (6 + 8)          (* g dela *)
= f 14               (* f dela *)
= 3 * 14             (* f dela *)
= 42
```

---

### Pri rekurzivnih funkcijah velikost sklada **hitro narašča**

.bad-example[```
let rec dolzina = function
  | [] -> 0
  | _ :: xs -> 1 + dolzina xs
```]

```
  dolzina [1; 2; 3]
= 1 + dolzina [2; 3]
= 1 + (1 + dolzina [3])
= 1 + (1 + (1 + dolzina []))
= 1 + (1 + (1 + 0))
= 1 + (1 + 1)
= 1 + 2
= 3
```

---

### Funkciji, kjer so vsi rekurzivni klici repni,<br>pravimo **repno rekurzivna**

.good-example[```
let rec dolzina' acc = function
  | [] -> acc
  | _ :: xs -> dolzina' (acc + 1) xs
```]

```
  dolzina' 0 [1; 2; 3]
= dolzina' (1 + 0) [2; 3]
= dolzina' 1 [2; 3]
= dolzina' (1 + 1) [3]
= dolzina' 2 [3]
= dolzina' (2 + 1) []
= dolzina' 3 []
= 3
```

---

class: center, middle, question

## `vsota` in `preslikaj`
### pretvori v repno rekurzivno

---

### OCaml **optimizira repne klice**

.source[```
let rec f x y =
    if y = 0 then x else f (x + 1) (y - 1)
```]

.terminal[```
# f 0 1000;;
- : int = 1000

# f 0 100000;;
- : int = 100000

# f 0 10000000;;
- : int = 10000000
```]

---

class: center, middle, question

## repna rekurzija v zbirniku

---

### Python **(nalašč) ne optimizira** repnih klicev

.source[```python
def f(x, y):
    if y == 0:
        return x
    else:
        return f(x + 1, y - 1)
```]

.terminal[```python
>>> f(0, 1000)
Traceback (most recent call last):
  File "<stdin>", line 1, in <module>
  File "<stdin>", line 5, in f
  ...
  File "<stdin>", line 2, in f
RecursionError: maximum recursion depth exceeded
```]

---

### Tudi v OCamlu bomo **prekoračili sklad**, če rekurzija ne bo repna

.source[```
let rec f y =
    if y = 0 then 0 else 1 + f (y - 1)
```]

.terminal[```
# f 1000;;
- : int = 1000

# f 100000;;
- : int = 100000

# f 10000000;;
Stack overflow during evaluation
  (looping recursion?).```]

---

class: center, middle

## Funkcije višjega reda

---

### Nekatere funkcije za argumente sprejemajo druge funkcije

.terminal[```
# let trikrat f x = f (f (f x));;
val trikrat : ('a -> 'a) -> 'a -> 'a = <fun>

# trikrat succ 39;;
- : int = 42

# trikrat sqrt 16.;;
- : float = 1.41421356237309515
```]

### Takim funkcijam pravimo, da so **višjega reda**

#### funkcija prvega reda … funkcija, ki sprejema osnovne vrednosti<br>funkcija reda \\(n\\) … funkcija, ki sprejema funkcije reda \\(n - 1\\)

---

class: center, middle, question

## [zanimivi](https://math.andrej.com/2006/03/21/interesting-higher-order-functionals/) primeri funkcij višjega reda

---

### **Anonimne funkcije** pišemo kot `fun x -> ...`

.terminal[```
# let moja_funkcija x = 2 * x + 1;;
val moja_funkcija : int -> int = <fun>

# trikrat moja_funkcija 1;;
- : int = 15

# trikrat (fun x -> 2 * x + 1) 1;;
- : int = 15

# let moja_funkcija' = fun x -> 2 * x + 1;;
val moja_funkcija' : int -> int = <fun>

# (fun f (x, y) -> f x * f y) succ (5, 6);;
- : int = 42
```]

---

### **Vgrajene funkcije** kličemo z `Modul.funkcija`

.terminal[```
# List.map String.length ["Ti"; "si"; "konj"];;
- : int list = [2; 2; 4]
```]

- osnovni modul [`Stdlib`](http://caml.inria.fr/pub/docs/manual-ocaml/libref/Stdlib.html)
- modul za nize [`String`](http://caml.inria.fr/pub/docs/manual-ocaml/libref/String.html)
- modul za sezname [`List`](http://caml.inria.fr/pub/docs/manual-ocaml/libref/List.html)
- seznam [vseh modulov](http://caml.inria.fr/pub/docs/manual-ocaml/libref/) v standardni knjižnici

---

class: center, middle

## Curryi<sup>?</sup>ranje

---

### Kaj se zgodi, če funkciji **dveh argumentov** podamo le **enega**?

.source[```
let zmnozi x y = x * y
```]

.terminal[<pre><code class="ocaml remark-code hljs"># zmnozi 2 3;;
- : int = 6

# zmnozi 2;;
.spoiler[\- : int -> int = &amp;lt;fun&amp;gt;]
</code></pre>]

--

### Ne dobimo napake, temveč **novo funkcijo**

---

### **Kakšno** funkcijo dobimo?

.terminal[```
# let f = zmnozi 2;;
val f : int -> int = <fun>

# f 3;;
- : int = 6
# f 10;;
- : int = 20
```]

.terminal[```
# (zmnozi 2) 3;;
- : int = 6      (* to je isto kot zmnozi 2 3 *)
# (zmnozi 2) 10;;
- : int = 20    (* to je isto kot zmnozi 2 10 *)
```]

---

class: center, middle

### funkcija *dveh* argumentov <br>=<br> funkcija *enega* argumenta (prvega), ki vrne<br> funkcijo *enega* argumenta (drugega)

- - -

### `f x y` = `(f x) y`

### `A -> B -> C` = `A -> (B -> C)`.

### `fun x y -> ...` = `fun x -> fun y -> ...`

- - -

### Takim funkcijam rečemo **curryi<sup>?</sup>rane** ([H. Curry](https://en.wikipedia.org/wiki/Haskell_Curry))

---

class: center, middle, question

### `f x y z` = .spoiler[`((f x) y) z`]

### `A -> B -> C -> D` = .spoiler[`A -> (B -> (C -> D))`]

### aplikacija je .spoiler[*levo*] asociativna

### `->` je .spoiler[*desno*] asociativna

---

### Zakaj ne uporabimo funkcije, ki sprejme par?

.terminal[```
# let zmnozi x y = x * y;;
val zmnozi : int -> int -> int = <fun>
# zmnozi 2 3;;
- : int = 6
# zmnozi 2 10;;
- : int = 20
```]

.terminal[```
# let zmnozi' (x, y) = x * y;;
val zmnozi' : int * int -> int = <fun>
# zmnozi' (2, 3);;
- : int = 6
# zmnozi' (2, 10);;
- : int = 20
```]

---

### Curryirane funkcije lahko **delno uporabimo**

.terminal[```
# preslikaj (zmnozi 2) [10; 20; 30];;
- : int list = [20; 40; 60]

# preslikaj ((+) 5) [10; 20; 30];;
- : int list = [15; 25; 35]

# preslikaj (preslikaj succ) [[10; 20]; [30]];;
- : int list list = [[11; 21]; [31]]
```]

---

### Tipa `A * B -> C` in `A -> B -> C` sta **izomorfna**

.terminal[```
# let curry f = fun x y -> f (x, y);;
val curry : ('a * 'b -> 'c) -> 'a -> 'b -> 'c = <fun>

# let uncurry g = fun (x, y) -> g x y;;
val uncurry : ('a -> 'b -> 'c) -> 'a * 'b -> 'c = <fun>```]

### $$C^{A \times B} \cong (C^B)^A$$

### $$curry \colon C^{A \times B} \to (C^B)^A, uncurry \colon (C^B)^A \to C^{A \times B}$$

### Postopku pravimo **curryi<sup>?</sup>ranje**, obratu **razcurryi<sup>?</sup>ranje**

---

class: center, middle

## `fold`

---

### Veliko funkcij na seznamih ima **enako strukturo**

.source[```
let rec vsota = function
  | [] -> 0
  | x :: xs -> x + vsota xs
```]

```
  vsota [1; 2; 3]
= 1 + vsota [2; 3]
= 1 + (2 + vsota [3])
= 1 + (2 + (3 + vsota []))
= 1 + (2 + (3 + 0))
```

---

### Veliko funkcij na seznamih ima **enako strukturo**

.source[```
let rec produkt = function
  | [] -> 1
  | x :: xs -> x * produkt xs
```]

```
  produkt [1; 2; 3]
= 1 * produkt [2; 3]
= 1 * (2 * produkt [3])
= 1 * (2 * (3 * produkt []))
= 1 * (2 * (3 * 1))
```

---

### Veliko funkcij na seznamih ima **enako strukturo**

.source[```
let rec dolzina = function
  | [] -> 0
  | _ :: xs -> 1 + dolzina xs
```]

```
  dolzina [1; 2; 3]
= 1 + dolzina [2; 3]
= 1 + (1 + dolzina [3])
= 1 + (1 + (1 + dolzina []))
= 1 + (1 + (1 + 0))
```

---

### Veliko funkcij na seznamih ima **enako strukturo**

.source[```
let rec preslikaj f = function
  | [] -> []
  | x :: xs -> f x :: preslikaj f xs
```]

```
  preslikaj f [1; 2; 3]
= f 1 : preslikaj f [2; 3]
= f 1 : (f 2 : preslikaj f [3])
= f 1 : (f 2 : (f 3 : preslikaj f []))
= f 1 : (f 2 : (f 3 : []))
```

---

### **Splošni vzorec** zajame funkcija `zlozi_desno`

.source[```
let rec zlozi_desno f xs z = match xs with
  | [] -> z
  | x :: xs -> f x (zlozi_desno f xs z)
```]

```
  zlozi_desno f [1; 2; 3] z
= f 1 (zlozi_desno f [2; 3] z)
= f 1 (f 2 (zlozi_desno f [3] z))
= f 1 (f 2 (f 3 (zlozi_desno f [] z)))
= f 1 (f 2 (f 3 z))
```

---

class: question, center, middle

### izrazi
## `vsota`, `produkt`, `dolzina`, `preslikaj`
### z uporabo
## `zlozi_desno`

---

### **Z druge strani** zlaga `zlozi_levo`

```
  zlozi_desno f [1; 2; 3] z
= f 1 (zlozi_desno f [2; 3] z)
= f 1 (f 2 (zlozi_desno f [3] z))
= f 1 (f 2 (f 3 (zlozi_desno f [] z)))
= f 1 (f 2 (f 3 z))
```

```
  zlozi_levo f z [1; 2; 3]
= zlozi_levo f (f z 1) [2; 3]
= zlozi_levo f (f (f z 1) 2) [3]
= zlozi_levo f (f (f (f z 1) 2) 3) []
= (f (f (f z 1) 2) 3)
```

#### `zlozi_levo` je tudi repno rekurzivna

---

class: question, center, middle

## `zlozi_levo`

.terminal.left[```
# zlozi_levo (^) "X" ["A"; "B"; "C"];;
- : string = "XABC"

# zlozi_levo (+) 0 [10; 20; 30];;
- : int = 60

# zlozi_levo (fun xs x -> x :: xs) [] [1; 2; 3];;
- : int list = [3; 2; 1]
```]

</textarea>
<script src="../../pomozno/prosojnice.js"></script>
</body>
</html>
