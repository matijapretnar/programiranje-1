{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "a751d058",
   "metadata": {},
   "source": [
    "# Curry-Howardov izomorfizem"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9b36ed96",
   "metadata": {},
   "source": [
    "[Curry-Howardov izomorfizem](https://en.wikipedia.org/wiki/Curry%E2%80%93Howard_correspondence) povezuje logiko s teorijo tipov. V grobem pravi, da lahko vsaki logični izjavi priredimo ustrezen tip v programskem jeziku, ki ima predstavnika natanko takrat, ko ima izjava dokaz. Curry-Howardov izomorfizem ni pomemben le zaradi globoke in lepe povezave dveh različnih področij, temveč ima tudi praktične posledice, saj na njem slonijo sodobni dokazovalni pomočniki, programi, ki matematične dokaze preverjajo na podoben način, kot OCaml preverja tipe."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d1bdeb32",
   "metadata": {},
   "source": [
    "## O množicah dokazov"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "93287746",
   "metadata": {},
   "source": [
    "Poglejmo si enostaven primer. Izjavo $P \\land Q$ lahko dokažemo tako, da dokažemo $P$ in $Q$ posebej. Obratno lahko iz $P \\land Q$ izpeljemo tako $P$ kot $Q$. Torej dokaz izjave $P \\land Q$ ustreza natanko paru dokazov za $P$ in za $Q$. Če z $\\mathcal{D}_R$ množico vseh dokazov izjave $R$, velja $\\mathcal{D}_{P \\land Q} \\simeq \\mathcal{D}_P \\times \\mathcal{D}_Q$.\n",
    "\n",
    "Če ne privzamemo aksioma o izključeni tretji možnosti, podobna povezava velja med disjunkcijo in vsoto. Izjavo $P \\lor Q$ dokažemo natanko takrat, kadar dokažemo $P$ ali dokažemo $Q$. Oziroma na krajše: $\\mathcal{D}_{P \\lor Q} \\simeq \\mathcal{D}_P + \\mathcal{D}_Q$.\n",
    "\n",
    "Zanimiva je tudi povezava med implikacijo in funkcijami. Izjavo $P \\Rightarrow Q$ dokažemo tako, da ob predpostavki $P$ dokažemo $Q$, kar pomeni, da je vsak dokaz izjave $P \\Rightarrow Q$ postopek, ki iz vsakega dokaza izjave $P$ izpelje dokaz izjave $Q$, oziroma $\\mathcal{D}_{P \\Rightarrow Q} \\simeq \\mathcal{D}_P \\to \\mathcal{D}_Q$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "60737040",
   "metadata": {},
   "source": [
    "Za primer vzemimo tavtologijo $(P \\land Q \\Rightarrow R) \\Rightarrow (P \\Rightarrow (Q \\Rightarrow R))$. Kako bi s pomočjo Curry-Howardovega izomorfizma preverili, da je resnična? Po zgornjih pravilih velja:\n",
    "\n",
    "$$\n",
    "\\mathcal{D}_{(P \\land Q \\Rightarrow R) \\Rightarrow (P \\Rightarrow (Q \\Rightarrow R))} \\simeq (\\mathcal{D}_P \\times \\mathcal{D}_Q \\to \\mathcal{D}_R) \\to (\\mathcal{D}_P \\to (\\mathcal{D}_Q \\to \\mathcal{D}_R)).\n",
    "$$\n",
    "\n",
    "Če nam uspe najti funkcijo $(\\mathcal{D}_P \\times \\mathcal{D}_Q \\to \\mathcal{D}_R) \\to (\\mathcal{D}_P \\to (\\mathcal{D}_Q \\to \\mathcal{D}_R))$ bomo našli tudi dokaz naše izjave. To je enostavno, saj gre le za konkreten primer izomorfizma $(A \\times B \\to C) \\simeq (A \\to (B \\to C))$, ki velja za poljubne množice $A, B, C$. Tako obstaja ne le preslikava ampak celo bijekcija $(\\mathcal{D}_P \\times \\mathcal{D}_Q \\to \\mathcal{D}_R) \\to (\\mathcal{D}_P \\to (\\mathcal{D}_Q \\to \\mathcal{D}_R))$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1465c3db",
   "metadata": {},
   "source": [
    "Za logično neresnico $\\bot$ seveda velja $\\mathcal{D}_\\bot = \\emptyset$, za logično resnico $\\top$ pa izberemo enojec $\\mathcal{D}_\\top = \\{*\\}$ z enim samim trivialnim dokazom, ki ga običajno označujemo z $*$.\n",
    "\n",
    "Za negacijo lahko iz $\\lnot P = P \\Rightarrow \\bot$ izpeljemo $\\mathcal{D}_{\\lnot P} = \\mathcal{D}_P \\to \\emptyset$. Izjava $\\lnot P$ bo tako imela dokaz takrat, kadar je množica njenih dokazov $\\mathcal{D}_P \\to \\emptyset$ neprazna. Množica preslikav v prazno množico pa je neprazna natanko takrat, kadar je prazna njena domena, v tem primeru $\\mathcal{D}_P$. Torej ima $\\lnot P$ dokaz natanko takrat, kadar ga $P$ nima."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "299352a6",
   "metadata": {},
   "source": [
    "## Povezava med izjavami in tipi"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bd20827d",
   "metadata": {},
   "source": [
    "V programskem jeziku z dovolj močnimi tipi, kot je OCaml, lahko predstavimo vse zgoraj opisane konstrukcije, kar nam omogoča, da dokazov ne pišemo samo \"na papir\", temveč v preverljivo programsko kodo.\n",
    "\n",
    "Na primer, če želimo pokazati, da izjava $(P \\land Q \\Rightarrow R) \\Rightarrow (P \\Rightarrow (Q \\Rightarrow R))$ drži za poljubne $P, Q, R$, je dovolj, da skonstruiramo polimorfno preslikavo tipa `(('p * 'q -> 'r) -> ('p -> ('q -> 'r)))`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "8d7a821c",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val dokaz_nase_izjave : ('p * 'q -> 'r) -> 'p -> 'q -> 'r = <fun>\n"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let dokaz_nase_izjave : (('p * 'q -> 'r) -> ('p -> ('q -> 'r))) =\n",
    "  fun dokaz_iz_p_in_q_sledi_r ->\n",
    "    fun dokaz_p ->\n",
    "      fun dokaz_q ->\n",
    "        let dokaz_p_in_q = (dokaz_p, dokaz_q) in\n",
    "        let dokaz_r = dokaz_iz_p_in_q_sledi_r dokaz_p_in_q in\n",
    "        dokaz_r"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ebfc9119",
   "metadata": {},
   "source": [
    "Če bi bili v dokazu malomarni, bi nas OCaml na to opozoril, saj se tipi ne bi ujemali:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "4bd64c6a",
   "metadata": {
    "tags": [
     "raises-exception"
    ],
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "ename": "error",
     "evalue": "compile_error",
     "output_type": "error",
     "traceback": [
      "File \"[17]\", line 3, characters 43-50:\n3 |     fun dokaz_p -> dokaz_iz_p_in_q_sledi_r dokaz_p\n                                               ^^^^^^^\nError: This expression has type 'p but an expression was expected of type\n         'p * 'q\n       The type variable 'p occurs inside 'p * 'q\n"
     ]
    }
   ],
   "source": [
    "let napacen_dokaz : (('p * 'q -> 'r) -> ('p -> ('q -> 'r))) =\n",
    "  fun dokaz_iz_p_in_q_sledi_r ->\n",
    "    fun dokaz_p -> dokaz_iz_p_in_q_sledi_r dokaz_p"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b5d4f545",
   "metadata": {},
   "source": [
    "Mimogrede: isto funkcijo bi lahko napisali tudi na krajše. V resnici smo jo že videli, gre za funkcijo `curry`, le imena parametrov so druga:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "1de66e83",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val curry : ('a * 'b -> 'c) -> 'a -> 'b -> 'c = <fun>\n"
      ]
     },
     "execution_count": 18,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let curry f x y = f (x, y)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a64c7460",
   "metadata": {},
   "source": [
    "V OCamlu imamo že vgrajene ustrezne tipe za konjunkcijo (produkt `*`), implikacijo (funkcijski tip `->`) in resnico (enotski tip `unit`). Za disjunkcijo lahko vsoto definiramo sami:\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "8fdca784",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "type ('p, 'q) sum = Left of 'p | Right of 'q\n"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "type ('p, 'q) sum = Left of 'p | Right of 'q"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a7ef4865",
   "metadata": {},
   "source": [
    "Dokaz izjave $((P \\lor Q) \\Rightarrow R) \\implies (P \\Rightarrow R) \\land (Q \\Rightarrow R)$ tako ustreza funkciji tipa `(('p, 'q) sum -> 'r) -> ('p -> 'r) * ('q -> 'r)`, ki jo lahko na primer definiramo kot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "4eb4d186",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val dokaz2 : (('p, 'q) sum -> 'r) -> ('p -> 'r) * ('q -> 'r) = <fun>\n"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let dokaz2 : (('p, 'q) sum -> 'r) -> ('p -> 'r) * ('q -> 'r) =\n",
    "  fun dokaz_iz_p_ali_q_sledi_r -> (\n",
    "    (fun dokaz_p -> dokaz_iz_p_ali_q_sledi_r (Left dokaz_p)),\n",
    "    (fun dokaz_q -> dokaz_iz_p_ali_q_sledi_r (Right dokaz_q))\n",
    "  )"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "125da7a3",
   "metadata": {},
   "source": [
    "Podobno lahko z naštevnim tipom brez konstruktorjev definiramo prazen tip `empty`, z njegovo pomočjo pa še tip za negacijo:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "f45d9406",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "type empty\n"
      ]
     },
     "execution_count": 19,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "type 'p not = 'p -> empty\n"
      ]
     },
     "execution_count": 19,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "type empty\n",
    "type 'p not = 'p -> empty"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "id": "fb900052",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val iz_p_sledi_ne_ne_p : 'p -> 'p not not = <fun>\n"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val iz_ne_p_ali_q_sledi_ne_p_in_ne_q : ('p, 'q) sum not -> 'p not * 'q not =\n",
       "  <fun>\n"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    },
    {
     "data": {
      "text/plain": [
       "val iz_ne_p_in_ne_q_sledi_ne_p_ali_q : 'p not * 'q not -> ('p, 'q) sum not =\n",
       "  <fun>\n"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let iz_p_sledi_ne_ne_p : 'p -> 'p not not = fun h_p h_ne_p -> h_ne_p h_p\n",
    "\n",
    "let iz_ne_p_ali_q_sledi_ne_p_in_ne_q : ('p, 'q) sum not -> 'p not * 'q not =\n",
    "  fun h_ne_p_ali_q -> (\n",
    "    (fun h_p -> h_ne_p_ali_q (Left h_p)),\n",
    "    (fun h_q -> h_ne_p_ali_q (Right h_q))\n",
    "  )\n",
    "  \n",
    "let iz_ne_p_in_ne_q_sledi_ne_p_ali_q : 'p not * 'q not -> ('p, 'q) sum not =\n",
    " fun (h_ne_p, h_ne_q) -> function\n",
    "  | Left h_p -> h_ne_p h_p\n",
    "  | Right h_q -> h_ne_q h_q"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "73bc1b7a",
   "metadata": {},
   "source": [
    "## Omejitve OCamla"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3c1ca75f",
   "metadata": {},
   "source": [
    "Na zgornji način nam lahko OCaml služi kot preprost dokazovalnik. Ima pa več omejitev:\n",
    "1. Pisanje dokazov s pomočjo funkcij je precej nerodno. Nekateri dokazovalniki (npr. Lean ali Rocq) tako namesto tega uporabljajo _taktike_, s pomočjo katerih računalniku dajemo le napotke, kakšne dokaze naj konstruira. Spet v drugih dokazovalnikih (npr. Agda ali Idris) pa dokaze res pišemo neposredno, vendar nam pri tem interaktivno pomaga urejevalnik.\n",
    "2. S tipi v OCamlu lahko izrazimo le formule v izjavnem računu, ki ne vsebujejo predikatov in kvantifikatorjev. Razmislimo, čemu bi ustrezal predikat $S$, ki pove, ali je naravno število sodo. Za vsako število $n$ imamo torej izjavo $S(n)$ in zato njej pripadajoč tip. Torej bi v OCamlu predikatu $S$ ustrezala funkcija, ki vsakemu številu priredi svoj tip! Pri lihih številih bi bil ta tip prazen, pri sodih pa ne. V splošnem bi potrebovali funkcije iz vrednosti v tipe. V OCamlu imamo običajne funkcije iz vrednosti v vrednosti in celo konstruktorje tipov (npr. `option`, `list`, …), ki slikajo tipe v tipe. Funkcij iz vrednosti v tipe pa žal nimamo.\n",
    "3. S pomočjo rekurzije lahko v OCamlu definiramo funkcije tipov, ki ustrezajo neresničnim izjavam. Na primer, izjava $P \\Rightarrow Q$ v splošnem ne drži, vendar lahko vedno napišemo:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "id": "fd17848d",
   "metadata": {
    "vscode": {
     "languageId": "ocaml"
    }
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "val iz_p_sledi_q : 'p -> 'q = <fun>\n"
      ]
     },
     "execution_count": 22,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "let rec iz_p_sledi_q : 'p -> 'q =\n",
    "  fun dokaz_p ->\n",
    "    let dokaz_q = iz_p_sledi_q dokaz_p in\n",
    "    dokaz_q"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f1a0042d",
   "metadata": {},
   "source": [
    "Res, če tej funkciji kot argument podamo dokaz za $P$, bo na koncu izvajanja funkcija vrnila dokaz za $Q$. Se pa znamo malo načakati…"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "OCaml default",
   "language": "OCaml",
   "name": "ocaml-jupyter"
  },
  "language_info": {
   "codemirror_mode": "text/x-ocaml",
   "file_extension": ".ml",
   "mimetype": "text/x-ocaml",
   "name": "OCaml",
   "nbconverter_exporter": null,
   "pygments_lexer": "OCaml",
   "version": "4.14.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
